plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'com.google.devtools.ksp' version '1.8.21-1.0.11'
}

apply from: "$rootDir/scripts/build/utils.gradle"
apply from: "$rootDir/scripts/publish/module.gradle"

def verCode, verName, verBuild, verNameShort, verPublish
(verCode, verName, verBuild, verNameShort, verPublish) = genVersion()

ext {
    PUBLISH_GROUP_ID = "com.plutolib"
    PUBLISH_VERSION = verPublish
    PUBLISH_ARTIFACT_ID = 'pluto'
}

android {
    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion

    buildFeatures {
        viewBinding true
    }


    defaultConfig {
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion

        buildConfigField "String", "VERSION_NAME", "\"${verPublish}\""
        buildConfigField ("long", "VERSION_CODE", "${verCode}")
        buildConfigField "String", "GIT_SHA", "\"${gitSha()}\""
    }

    buildTypes {
        release {
            debuggable true
            minifyEnabled false
            shrinkResources false
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    resourcePrefix 'pluto___'
    namespace 'com.pluto'
    lint {
        abortOnError false
    }
}

dependencies {
    api project(path: ':pluto-plugins:base:lib')

    implementation "androidx.core:core-ktx:$androidXCoreVersion"
    implementation 'androidx.appcompat:appcompat:1.4.0'

    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-moshi:2.9.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.1'
    implementation 'com.squareup.okhttp3:okhttp:4.9.1'
    implementation 'com.squareup.okio:okio:2.10.0'

    implementation "com.squareup.moshi:moshi:$moshiVersion"
    ksp "com.squareup.moshi:moshi-kotlin-codegen:$moshiVersion"
}