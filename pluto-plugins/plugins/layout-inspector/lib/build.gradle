plugins {
    alias(libs.plugins.android.library)
    alias(libs.plugins.kotlin.android)
    alias(libs.plugins.kotlin.parcelize)
}

apply from: "$rootDir/scripts/build/utils.gradle"
apply from: "$rootDir/scripts/publish/module.gradle"

def verCode, verName, verBuild, verNameShort, verPublish
(verCode, verName, verBuild, verNameShort, verPublish) = genVersion()

ext {
    PUBLISH_GROUP_ID = "com.plutolib.plugins"
    PUBLISH_VERSION = verPublish
    PUBLISH_ARTIFACT_ID = 'layout-inspector'
}

android {
    compileSdk = libs.versions.compileSdk.get().toInteger()
    buildToolsVersion = libs.versions.buildTools.get()

    buildFeatures {
        viewBinding true
    }


    defaultConfig {
        minSdk = libs.versions.minSdk.get().toInteger()
        targetSdk = libs.versions.targetSdk.get().toInteger()

        buildConfigField "String", "VERSION_NAME", "\"${verPublish}\""
        buildConfigField ("long", "VERSION_CODE", "${verCode}")
        buildConfigField "String", "GIT_SHA", "\"${gitSha()}\""
    }

    buildTypes {
        release {
            debuggable true
            minifyEnabled false
            shrinkResources false
        }
    }

    compileOptions {
        sourceCompatibility = JavaVersion.toVersion(libs.versions.java.get())
        targetCompatibility = JavaVersion.toVersion(libs.versions.java.get())
    }

    kotlinOptions {
        jvmTarget = libs.versions.java.get()
    }

    resourcePrefix 'pluto_li___'
    namespace 'com.pluto.plugins.layoutinspector'
    lint {
        abortOnError false
    }
}

dependencies {
    implementation project(path: ':pluto-plugins:base:lib')
    implementation(libs.androidx.core)
}