plugins {
    alias(libs.plugins.android.library)
    alias(libs.plugins.kotlin.android)
    alias(libs.plugins.ksp)
}

apply from: "$rootDir/scripts/build/utils.gradle"
apply from: "$rootDir/scripts/publish/module.gradle"

def verCode, verName, verBuild, verNameShort, verPublish
(verCode, verName, verBuild, verNameShort, verPublish) = genVersion()

ext {
    PUBLISH_GROUP_ID = "com.plutolib.plugins"
    PUBLISH_VERSION = verPublish
    PUBLISH_ARTIFACT_ID = 'preferences'
}

android {
    compileSdk = libs.versions.compileSdk.get().toInteger()
    buildToolsVersion = libs.versions.buildTools.get()

    buildFeatures {
        viewBinding true
    }


    defaultConfig {
        minSdk = libs.versions.minSdk.get().toInteger()
        targetSdk = libs.versions.targetSdk.get().toInteger()

        buildConfigField "String", "VERSION_NAME", "\"${verPublish}\""
    }

    buildTypes {
        release {
            debuggable true
            minifyEnabled false
            shrinkResources false
        }
    }

    compileOptions {
        sourceCompatibility = JavaVersion.toVersion(libs.versions.java.get())
        targetCompatibility = JavaVersion.toVersion(libs.versions.java.get())
    }

    kotlinOptions {
        jvmTarget = libs.versions.java.get()
    }

    resourcePrefix 'pluto_pref___'
    namespace 'com.pluto.plugins.preferences'
    lint {
        abortOnError false
    }
}

dependencies {
//    implementation 'com.plutolib:plugin:2.0.0'
    implementation project(path: ':pluto-plugins:base:lib')
    implementation(libs.androidx.core)
    implementation(libs.androidx.navigation.ui)
    implementation(libs.google.material)
    implementation(libs.androidx.navigation.fragment)
    implementation(libs.androidx.preference)

    implementation(libs.moshi)
    ksp(libs.moshi.codegen)
}

task validateChanges {
    dependsOn 'ktlintFormat'
    dependsOn 'detekt'
}