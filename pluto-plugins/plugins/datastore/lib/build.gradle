plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
}

apply from: "$rootDir/scripts/build/utils.gradle"
apply from: "$rootDir/scripts/publish/module.gradle"

def verCode, verName, verBuild, verNameShort, verPublish
(verCode, verName, verBuild, verNameShort, verPublish) = genVersion()

ext {
    PUBLISH_GROUP_ID = "com.plutolib.plugins"
    PUBLISH_VERSION = verPublish
    PUBLISH_ARTIFACT_ID = 'datastore-pref'
}

android {
    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion

    buildFeatures {
        viewBinding true
        compose true
    }


    defaultConfig {
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion

        buildConfigField "String", "VERSION_NAME", "\"${verPublish}\""
        buildConfigField("long", "VERSION_CODE", "${verCode}")
        buildConfigField "String", "GIT_SHA", "\"${gitSha()}\""
    }

    buildTypes {
        release {
            debuggable true
            minifyEnabled false
            shrinkResources false
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    composeOptions {
        kotlinCompilerExtensionVersion = composeCompileVersion
    }

    resourcePrefix 'pluto_dts___'
    namespace 'com.pluto.plugins.datastore.pref'
    lint {
        abortOnError false
    }
}

dependencies {
    implementation project(path: ':pluto-plugins:base:lib')
    implementation "androidx.core:core-ktx:$androidXCoreVersion"
    implementation "androidx.compose.material3:material3:$composeMaterial3Version"
    implementation "androidx.compose.foundation:foundation:$composeVersion"
    implementation "androidx.compose.runtime:runtime:$composeVersion"
    implementation "androidx.compose.ui:ui:$composeVersion"
    implementation "androidx.compose.ui:ui-tooling-preview:$composeVersion"
    implementation "androidx.compose.ui:ui-tooling:$composeVersion"
    implementation "androidx.constraintlayout:constraintlayout-compose:1.0.1"

    implementation "androidx.datastore:datastore-preferences:1.0.0"

}