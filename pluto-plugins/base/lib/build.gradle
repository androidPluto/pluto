plugins {
    alias(libs.plugins.android.library)
    alias(libs.plugins.kotlin.android)
}

apply from: "$rootDir/scripts/build/utils.gradle"
apply from: "$rootDir/scripts/publish/module.gradle"

def verCode, verName, verBuild, verNameShort, verPublish
(verCode, verName, verBuild, verNameShort, verPublish) = genVersion()

ext {
    PUBLISH_GROUP_ID = "com.plutolib"
    PUBLISH_VERSION = verPublish
    PUBLISH_ARTIFACT_ID = 'plugin'
}

android {
    compileSdk = libs.versions.compileSdk.get().toInteger()
    buildToolsVersion = libs.versions.buildTools.get()

    buildFeatures {
        viewBinding true
    }

    defaultConfig {
        minSdk = libs.versions.minSdk.get().toInteger()
        targetSdk = libs.versions.targetSdk.get().toInteger()
    }

    buildTypes {
        release {
            debuggable true
            minifyEnabled false
            shrinkResources false
        }
    }
    compileOptions {
        sourceCompatibility = JavaVersion.toVersion(libs.versions.java.get())
        targetCompatibility = JavaVersion.toVersion(libs.versions.java.get())
    }
    kotlinOptions {
        jvmTarget = libs.versions.java.get()
    }

    resourcePrefix 'pluto___'
    namespace 'com.pluto.plugin'
    lint {
        abortOnError false
    }
}

dependencies {
    implementation(libs.androidx.core)
    implementation(libs.androidx.appcompat)

    api(libs.androidx.constraintlayout)
    api(libs.androidx.navigation.fragment)
    api(libs.androidx.recyclerview)

    api(libs.google.material)

    api(libs.androidx.cardview)

    api(libs.androidx.lifecycle.common)
    api(libs.androidx.lifecycle.runtime)
}