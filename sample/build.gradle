plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'com.google.devtools.ksp' version '1.8.21-1.0.11'
    id 'org.jetbrains.kotlin.plugin.serialization' version "1.8.21"
}

apply from: "$rootDir/scripts/build/utils.gradle"

def verCode, verName, verBuild, verNameShort, verPublish
(verCode, verName, verBuild, verNameShort, verPublish) = genVersion()

android {
    compileSdkVersion 33
    buildToolsVersion "33.0.0"

    buildFeatures {
        viewBinding true
    }

    defaultConfig {
        applicationId "com.sampleapp"
        minSdkVersion 21
        targetSdkVersion 33
        versionCode verCode
        versionName verPublish

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        /**
         * build type with lib modules
         */
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debugMaven {
            initWith debug
            signingConfig signingConfigs.debug
            matchingFallbacks = ['debug']
        }

        /**
         * build type with lib-no-op modules
         */
        debugNoOp {
            initWith debug
            signingConfig signingConfigs.debug
            matchingFallbacks = ['debug']
        }
        debugNoOpMaven {
            initWith debug
            signingConfig signingConfigs.debug
            matchingFallbacks = ['debug']
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }
    namespace 'com.sampleapp'
}

dependencies {
    /* Local Op dependencies */
    debugImplementation project(path: ':pluto:lib')
    debugImplementation project(path: ':pluto-plugins:bundle:lib')

    /* Maven Op dependencies */
    debugMavenImplementation "com.plutolib:pluto:$verPublish"
    debugMavenImplementation "com.plutolib.plugins:bundle-core:$verPublish"

    /* Local NoOp dependencies */
    debugNoOpImplementation project(path: ':pluto:lib-no-op')
    debugNoOpImplementation project(path: ':pluto-plugins:bundle:lib-no-op')

    /* Maven NoOp dependencies */
    debugNoOpMavenImplementation "com.plutolib:pluto-no-op:$verPublish"
    debugNoOpMavenImplementation "com.plutolib.plugins:bundle-core-no-op:$verPublish"

    releaseImplementation "com.plutolib:pluto:$verPublish"
    releaseImplementation "com.plutolib.plugins:bundle-core:$verPublish"

    /**
     * Other dependencies
     */
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
    implementation 'androidx.appcompat:appcompat:1.4.2'
    implementation "androidx.core:core-ktx:$androidXCoreVersion"
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'com.google.android.material:material:1.6.1'
    implementation 'androidx.annotation:annotation:1.4.0'

    implementation 'androidx.lifecycle:lifecycle-common-java8:2.5.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.0'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.5.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.6.0-alpha01'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.0"

    /**
     * Network plugin test dependency
     */
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-moshi:2.9.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.1'

    def ktor_version = "2.3.2"
    implementation "io.ktor:ktor-client-core-jvm:$ktor_version"
    implementation "io.ktor:ktor-client-android:$ktor_version"
    implementation "io.ktor:ktor-client-content-negotiation:$ktor_version"
    implementation "io.ktor:ktor-serialization-kotlinx-json:$ktor_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.5.1'


    implementation 'com.squareup.okhttp3:okhttp:4.9.1'
    implementation 'com.squareup.okio:okio:2.10.0'

    implementation "com.squareup.moshi:moshi:$moshiVersion"
    ksp "com.squareup.moshi:moshi-kotlin-codegen:$moshiVersion"

    implementation "androidx.datastore:datastore-preferences:1.0.0"
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.7'

    implementation "androidx.navigation:navigation-fragment-ktx:$navigationVersion"

    implementation "androidx.room:room-ktx:$roomsVersion"
    ksp "androidx.room:room-compiler:$roomsVersion"
}
