// Top-level build file where you can add configuration options common to all sub-projects/modules.
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'io.github.gradle-nexus.publish-plugin'

buildscript {
    apply from: 'dependencies.gradle'
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
        google()
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.3.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "com.github.dcendents:android-maven-gradle-plugin:2.1"

        // maven central dependencies
        classpath 'io.github.gradle-nexus:publish-plugin:1.1.0'
        classpath 'org.jetbrains.dokka:dokka-gradle-plugin:1.4.30'

        // ktlint & detekt dependencies
        classpath "org.jlleitschuh.gradle:ktlint-gradle:10.2.0"
        classpath "io.gitlab.arturbosch.detekt:detekt-gradle-plugin:$detektVersion"
    }
}

allprojects {
    repositories {
        google()
        jcenter()

        // for testing Pluto staged repository
        maven { url "https://s01.oss.sonatype.org/content/groups/staging/" }
    }
}

subprojects {
//    if (project.name != "pluto-no-op") {
    pluginManager.withPlugin('kotlin-android') {
        apply from: "$rootDir/scripts/static-analysis/code-analysis.gradle"
    }
//    }
}

task installGitHook(type: Copy) {
    from new File(rootProject.rootDir, 'pre-commit')
    into { new File(rootProject.rootDir, '.git/hooks') }
    fileMode 0777
}

task clean(type: Delete) {
    dependsOn(installGitHook)
    delete rootProject.buildDir
}

tasks.withType(io.gitlab.arturbosch.detekt.Detekt).configureEach {
    exclude(".*/resources/.*,.*/build/.*")
}

task prCheck {
    dependsOn ':sample:assembleDebug'
    dependsOn ':pluto:validateChanges'
}

apply from: "$rootDir/scripts/publish/root.gradle"
apply from: "$rootDir/scripts/project-dependancy-graph.gradle"
apply from: "$rootDir/maven-versions.gradle"