// Top-level build file where you can add configuration options common to all sub-projects/modules.
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'io.github.gradle-nexus.publish-plugin'

buildscript {
    ext.kotlin_version = '1.5.21'
    ext.detekt_version = '1.19.0'
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
        google()
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.0.4'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.5.21"
        classpath "com.github.dcendents:android-maven-gradle-plugin:2.1"

        // maven central dependencies
        classpath 'io.github.gradle-nexus:publish-plugin:1.1.0'
        classpath 'org.jetbrains.dokka:dokka-gradle-plugin:1.4.30'

        // ktlint & detekt dependencies
        classpath "org.jlleitschuh.gradle:ktlint-gradle:10.2.0"
        classpath "io.gitlab.arturbosch.detekt:detekt-gradle-plugin:$detekt_version"
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

subprojects {
//    if (project.name != "pluto-no-op") {
        pluginManager.withPlugin('kotlin-android') {
            apply from: "$rootDir/scripts/static-analysis/code-analysis.gradle"
        }
//    }
}

ext {
    minSdkVersion = 21
    targetSdkVersion = 32
    compileSdkVersion = 32
    buildToolsVersion = '32.0.0'

    androidXCoreVersion = '1.7.0'
}

task installGitHook(type: Copy) {
    from new File(rootProject.rootDir, 'pre-commit')
    into { new File(rootProject.rootDir, '.git/hooks') }
    fileMode 0777
}

task clean(type: Delete) {
    dependsOn(installGitHook)
    delete rootProject.buildDir
}

tasks.withType(io.gitlab.arturbosch.detekt.Detekt).configureEach {
    exclude(".*/resources/.*,.*/build/.*")
}

task prCheck {
    dependsOn ':sample:assembleDebug'
    dependsOn ':pluto:validateChanges'
}

apply from: "$rootDir/scripts/publish/root.gradle"
apply from: "$rootDir/scripts/project-dependancy-graph.gradle"