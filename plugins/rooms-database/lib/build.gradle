plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'kotlin-kapt'
}

apply from: "$rootDir/scripts/build/utils.gradle"
apply from: "$rootDir/scripts/publish/module.gradle"

def verCode, verName, verBuild, verNameShort, verPublish
(verCode, verName, verBuild, verNameShort, verPublish) = genVersion()

ext {
    PUBLISH_GROUP_ID = "com.plutolib.plugins"
    PUBLISH_VERSION = verPublish
    PUBLISH_ARTIFACT_ID = 'rooms-db'
}

android {
    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion

    buildFeatures {
        viewBinding true
    }

    lintOptions {
        abortOnError false
    }

    defaultConfig {
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode verCode
        versionName verName

        buildConfigField "String", "VERSION_NAME", "\"${verPublish}\""
        buildConfigField ("long", "VERSION_CODE", "${verCode}")
        buildConfigField "String", "GIT_SHA", "\"${gitSha()}\""
    }

    buildTypes {
        release {
            debuggable true
            minifyEnabled false
            shrinkResources false
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    resourcePrefix 'pluto_rooms___'
}

dependencies {
//    implementation 'com.plutolib:plugin:2.0.0'
    implementation project(path: ':pluto-plugin')
    implementation "androidx.core:core-ktx:$androidXCoreVersion"
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'

    implementation "androidx.room:room-ktx:$roomsVersion"
    kapt "androidx.room:room-compiler:$roomsVersion"
}